#pragma once

#include "Common.ush"

#define USE_LOW_RESLUTION   1

#if USE_LOW_RESLUTION
	#define HIZ_SIZE_WIDTH 256
	#define HIZ_SIZE_HEIGHT 128
    #define HIZ_SIZE_WIDTH_FLOAT 256.f
    #define HIZ_SIZE_HEIGHT_FLOAT 128.f
	#define HIZ_BUFFER_WIDTH 255.f
	#define HIZ_BUFFER_HEIGHT 127.f
	#define MaxMipLevel 7.f
#else
	#define HIZ_SIZE_WIDTH 512.f
	#define HIZ_SIZE_HEIGHT 256.f
	#define HIZ_BUFFER_WIDTH 511.f
	#define HIZ_BUFFER_HEIGHT 255.f
	#define MaxMipLevel 8.f
#endif

// Rect is inclusive [Min.xy, Max.xy]
int MipLevelForRect(int4 RectPixels, int DesiredFootprintPixels)
{
    const int MaxPixelOffset = DesiredFootprintPixels - 1;
    const int MipOffset = (int) log2((float) DesiredFootprintPixels) - 1;

	// Calculate lowest mip level that allows us to cover footprint of the desired size in pixels.
	// Start by calculating separate x and y mip level requirements.
	// 2 pixels of mip k cover 2^(k+1) pixels of mip 0. To cover at least n pixels of mip 0 by two pixels of mip k we need k to be at least k = ceil( log2( n ) ) - 1.
	// For integer n>1: ceil( log2( n ) ) = floor( log2( n - 1 ) ) + 1.
	// So k = floor( log2( n - 1 )
	// For integer n>1: floor( log2( n ) ) = firstbithigh( n )
	// So k = firstbithigh( n - 1 )
	// As RectPixels min/max are both inclusive their difference is one less than number of pixels (n - 1), so applying firstbithigh to this difference gives the minimum required mip.
	// NOTE: firstbithigh is a FULL rate instruction on GCN while log2 is QUARTER rate instruction.
    
    //GLSL findMSB or findLSB
    int2 MipLevelXY = firstbithigh(RectPixels.zw - RectPixels.xy);
    
	// Mip level needs to be big enough to cover both x and y requirements. Go one extra level down for 4x4 sampling.
	// firstbithigh(0) = -1, so clamping with 0 here also handles the n=1 case where mip 0 footprint is just 1 pixel wide/tall.
    int MipLevel = max(max(MipLevelXY.x, MipLevelXY.y) - MipOffset, 0);

	// MipLevel now contains the minimum MipLevel that can cover a number of pixels equal to the size of the rectangle footprint, but the HZB footprint alignments are quantized to powers of two.
	// The quantization can translate down the start of the represented range by up to 2^k-1 pixels, which can decrease the number of usable pixels down to 2^(k+1) - 2^k-1.
	// Depending on the alignment of the rectangle this might require us to pick one level higher to cover all rectangle footprint pixels.
	// Note that testing one level higher is always enough as this guarantees 2^(k+2) - 2^k usable pixels after alignment, which is more than the 2^(k+1) required pixels.
	
	// Transform coordinates down to coordinates of selected mip level and if they are not within reach increase level by one.
    MipLevel += any((RectPixels.zw >> MipLevel) - (RectPixels.xy >> MipLevel) > MaxPixelOffset) ? 1 : 0;

    return MipLevel;
}

float GetMinDepthFromHZB(/*float2 HZBSize, */uint4 SamplePosition, uint HZBLevel, Texture2D<float> HZBTexture, SamplerState HZBSampler)
{
    //#TODO: GatherLODRed
    float4 Depth;
    Depth.x = HZBTexture.Load(uint3(SamplePosition.x, SamplePosition.w, HZBLevel)); // (-,+)
    Depth.y = HZBTexture.Load(uint3(SamplePosition.z, SamplePosition.w, HZBLevel)); // (+,+)
    Depth.z = HZBTexture.Load(uint3(SamplePosition.z, SamplePosition.y, HZBLevel)); // (+,-)
    Depth.w = HZBTexture.Load(uint3(SamplePosition.x, SamplePosition.y, HZBLevel)); // (-,-)
    
    //Depth.yz = (SamplePosition.x == SamplePosition.z) ? 1.0f : Depth.yz; // Mask off right pixels, if footprint is only one pixel wide.
    //Depth.xy = (SamplePosition.y == SamplePosition.w) ? 1.0f : Depth.xy; // Mask off bottom pixels, if footprint is only one pixel tall.
    
    float2 Depth_0 = min(Depth.xy, Depth.zw);
    float MinDepth = min(Depth_0.x, Depth_0.y);
    return MinDepth;
}

float GetMinDepthFromHZBDownSample(uint4 SamplePosition, uint2 CenterSamplePos, uint SampleLevel, Texture2D<float> HZBTexture, SamplerState HZBSampler)
{
    float4 Depth;
    Depth.x = HZBTexture.Load(int3(SamplePosition.x, SamplePosition.y, SampleLevel));
    Depth.y = HZBTexture.Load(int3(SamplePosition.z, SamplePosition.w, SampleLevel));
    Depth.z = HZBTexture.Load(int3(SamplePosition.x, SamplePosition.w, SampleLevel));
    Depth.w = HZBTexture.Load(int3(SamplePosition.z, SamplePosition.y, SampleLevel));
    float CenterDepth = HZBTexture.Load(int3(CenterSamplePos.xy, SampleLevel));
    float2 Depth_0 = min(Depth.xy, Depth.zw);
    float Depth_1 = min(Depth_0.x, Depth_0.y);
    float Depth_2 = min(Depth_1, CenterDepth);
    
    return Depth_2;
}

int4 GetScreenRect(int4 ViewRect, float4 NDCRect)
{
    // Map from NDC [-1,1] to target 'texture UV' [0,1] space, X[-1,1] -> [0,1], Y[-1,1] -> [1, 0]
	// CF DX11.3 Functional Spec 3.3.1 Pixel Coordinate System
    float4 RectUV = saturate(NDCRect * float2(0.5f, -0.5f).xyxy + 0.5f).xwzy;
    
	// Calculate pixel footprint of rectangle in full resolution.
	// To make the bounds as tight as possible we only consider a pixel part of the footprint when its pixel center is covered by the rectangle.
	// Only when the pixel center is covered can that pixel be rasterized by anything inside the rectangle.
	// Using pixel centers instead of conservative floor/ceil bounds of pixel seems to typically result in ~5% fewer clusters being drawn.
	// NOTE: This assumes anything inside RectMin/RectMax gets rasterized with one centered sample. This will have to be adjusted for conservative rasterization, MSAA or similar features.
    float2 ViewSize = ViewRect.zw - ViewRect.xy;
    
    //Nanite这里是将RectMax偏移半个像素, RectMin偏移半个像素, 若大于等于则代表覆盖, 与寒霜不同的是额外偏移后用来检查Size是否超过半个像素来判断是否应该光栅
    //因为我们主要是需要屏幕空间的Rect信息, 所以使用Nanite算法
    //这个算法如果Bound与起始位置刚好相交, 不会光栅起始三角形, 若起始位置不相加则可以正常光栅
    int4 Pixels = int4(RectUV * ViewSize.xyxy + ViewRect.xyxy + float4(0.5f, 0.5f, -0.5f, -0.5f));
    //int4 Pixels = int4(RectUV * ViewSize.xyxy + ViewRect.xyxy + float4(0.f, 0.f, -0.5f, -0.5f)); 
    
    //Clamp不会影响越界情况, 依然能够正常判断, 因为起始值并没有被Clamp只是判断了最小值
    Pixels.xy = max(Pixels.xy, ViewRect.xy);
    Pixels.zw = min(Pixels.zw, ViewRect.zw - 1);
    
    return Pixels;
}

bool IsVisibleHZB(Texture2D<float> HZBTexture, SamplerState HZBSampler, int4 ViewRect, float4 NDCRect, float MaxZ)
{
    int4 LastFrameScreenPixels = GetScreenRect(ViewRect, NDCRect);
    int4 HZBTexels = int4(LastFrameScreenPixels.xy, max(LastFrameScreenPixels.xy, LastFrameScreenPixels.zw)); //前面执行了+-0.5, 未做PixelCoverTest的情况下必须判断
    HZBTexels >>= 1;
            
    uint HZBLevel = MipLevelForRect(HZBTexels, 2);
    HZBTexels >>= HZBLevel; // Transform HZB Mip 0 coordinates to coordinates of selected Mip level.
            
    float MinDepth = GetMinDepthFromHZB(HZBTexels, HZBLevel, HZBTexture, HZBSampler);     
    return MinDepth <= MaxZ;
}

bool IsVisibleHZBDownSample(Texture2D<float> HZBTexture, SamplerState HZBSampler, float4 NDCRect, float MaxZ)
{
    int4 ViewRect = int4(0, 0, HIZ_SIZE_WIDTH, HIZ_SIZE_HEIGHT);
    float4 RectUV = saturate(NDCRect * float2(0.5f, -0.5f).xyxy + 0.5f).xwzy;
    float2 ViewSize = float2(HIZ_SIZE_WIDTH_FLOAT, HIZ_SIZE_HEIGHT_FLOAT);
    int4 Pixels = int4(RectUV * ViewSize.xyxy);
    Pixels.xy = max(Pixels.xy, ViewRect.xy);
    Pixels.zw = min(Pixels.zw, ViewRect.zw - 1);
    //int4 HZBTexels = Pixels >> 1;
    
    uint HZBLevel = MipLevelForRect(Pixels, 2);
    int4 HZBTexels = Pixels >> HZBLevel;
    //HZBTexels >>= HZBLevel;

    float MinDepth = GetMinDepthFromHZB(HZBTexels, HZBLevel, HZBTexture, HZBSampler);
    return MinDepth <= MaxZ;
}

bool IsVisibleHZBDownSampleUnreal4(Texture2D<float> HZBTexture, SamplerState HZBSampler, float4 NDCRect, float MaxZ)
{
    float4 Rect = (NDCRect * float2(0.5, -0.5).xyxy + float4(0.5, 0.5, 0.5, 0.5)).xwzy; //超出屏幕外时不应该使用saturate,无法计算正确的大小
    float4 RectPixels = Rect * float4(HIZ_SIZE_WIDTH_FLOAT, HIZ_SIZE_HEIGHT_FLOAT, HIZ_SIZE_WIDTH_FLOAT, HIZ_SIZE_HEIGHT_FLOAT);
    float2 RectSize = (RectPixels.zw - RectPixels.xy) * 0.5; // + 0.001 *0.5 表示level - 1,因为第0级也是一层
    float Level = min(max(ceil(log2(max(RectSize.x, RectSize.y))), 0.f), MaxMipLevel);
    uint SampleLevel = uint(Level);
    
    //https://docs.microsoft.com/en-us/windows/win32/direct3d9/nearest-point-sampling
    //纹理下标应当映射到[-0.5, n - 0.5]
    float4 SamplePosition = Rect * float4(HIZ_SIZE_WIDTH, HIZ_SIZE_HEIGHT, HIZ_SIZE_WIDTH, HIZ_SIZE_HEIGHT) - 0.5f;
    SamplePosition = max(SamplePosition, float4(0.f, 0.f, 0.f, 0.f));
    SamplePosition = min(SamplePosition, float4(HIZ_BUFFER_WIDTH, HIZ_BUFFER_HEIGHT, HIZ_BUFFER_WIDTH, HIZ_BUFFER_HEIGHT));
    uint4 MaxSamplePos = round(SamplePosition);
    uint4 CurSamplePos = MaxSamplePos >> SampleLevel;
    
    //float FurthestDepth = GetDepthFromBuffer(HZBResource, CurSamplePos, CenterSamplePos, SampleLevel);
    //float FurthestDepth = GetMinDepthFromHZBDownSample(CurSamplePos, CenterSamplePos, SampleLevel, HZBResource, HZBTextureSampler);
    float MinDepth = GetMinDepthFromHZB(CurSamplePos, SampleLevel, HZBTexture, HZBSampler);
    return MinDepth <= MaxZ;
}

bool IsVisibleHZBStorageBufferDownSampleUnreal4(StructuredBuffer<float> HZBBuffer, float4 NDCRect, float MaxZ)
{
    float4 Rect = (NDCRect * float2(0.5, -0.5).xyxy + float4(0.5, 0.5, 0.5, 0.5)).xwzy; //超出屏幕外时不应该使用saturate,无法计算正确的大小
    float4 RectPixels = Rect * float4(HIZ_SIZE_WIDTH_FLOAT, HIZ_SIZE_HEIGHT_FLOAT, HIZ_SIZE_WIDTH_FLOAT, HIZ_SIZE_HEIGHT_FLOAT);
    float2 RectSize = (RectPixels.zw - RectPixels.xy) * 0.5; // + 0.001 *0.5 表示level - 1,因为第0级也是一层
    float Level = min(max(ceil(log2(max(RectSize.x, RectSize.y))), 0.f), MaxMipLevel);
    uint SampleLevel = uint(Level);
    
    //https://docs.microsoft.com/en-us/windows/win32/direct3d9/nearest-point-sampling
    //纹理下标应当映射到[-0.5, n - 0.5]
    float4 SamplePosition = Rect * float4(HIZ_SIZE_WIDTH, HIZ_SIZE_HEIGHT, HIZ_SIZE_WIDTH, HIZ_SIZE_HEIGHT) - 0.5f;
    SamplePosition = max(SamplePosition, float4(0.f, 0.f, 0.f, 0.f));
    SamplePosition = min(SamplePosition, float4(HIZ_BUFFER_WIDTH, HIZ_BUFFER_HEIGHT, HIZ_BUFFER_WIDTH, HIZ_BUFFER_HEIGHT));
    uint4 MaxSamplePos = round(SamplePosition);
    uint4 CurSamplePos = MaxSamplePos >> SampleLevel;
    
    //float FurthestDepth = GetDepthFromBuffer(HZBResource, CurSamplePos, CenterSamplePos, SampleLevel);
    //float FurthestDepth = GetMinDepthFromHZBDownSample(CurSamplePos, CenterSamplePos, SampleLevel, HZBResource, HZBTextureSampler);
#if USE_LOW_RESLUTION
    uint2 OffsetAndSizeArray[8] =
    {
        uint2(0u, 256u),
        uint2(0x8000u, 128u),
        uint2(0xA000u, 64u),
        uint2(0xA800u, 32u),
        uint2(0xAA00u, 16u),
        uint2(0xAA80u, 8u),
        uint2(0xAAA0u, 4u),
        uint2(0xAAA8u, 2u)
    };
#else
    uint2 OffsetAndSizeArray[9] =
    {
        uint2(0, 512u),
		uint2(0x20000, 256u),
		uint2(0x28000, 128u),
		uint2(0x2A000, 64u),
		uint2(0x2A800, 32u),
		uint2(0x2AA00, 16u),
		uint2(0x2AA80, 8u),
		uint2(0x2AAA0, 4u),
		uint2(0x2AAA8, 2u),
    };
#endif

    
    uint2 OffsetAndSize = OffsetAndSizeArray[SampleLevel];  
    //uint LocalIndex_0 = OffsetAndSize.y * CurSamplePos.y + CurSamplePos.x;
    //uint GlobalIndex_0 = LocalIndex_0 + OffsetAndSize.x;
    //uint LocalIndex_1 = OffsetAndSize.y * CurSamplePos.w + CurSamplePos.z;
    //uint GlobalIndex_1 = LocalIndex_1 + OffsetAndSize.x; 
    //uint LocalIndex_2 = OffsetAndSize.y * CurSamplePos.y + CurSamplePos.z;
    //uint GlobalIndex_2 = LocalIndex_2 + OffsetAndSize.x;
    //uint LocalIndex_3 = OffsetAndSize.y * CurSamplePos.w + CurSamplePos.x;
    //uint GlobalIndex_3 = LocalIndex_3 + OffsetAndSize.x;
    //uint LocalIndex_4 = OffsetAndSize.y * CenterSamplePos.y + CenterSamplePos.x;
    //uint GlobalIndex_4 = LocalIndex_4 + OffsetAndSize.x;
    uint4 LocalIndex = CurSamplePos.yyww * OffsetAndSize.yyyy + CurSamplePos.xzxz;
    uint4 GlobalIndex = LocalIndex + OffsetAndSize.xxxx;
    
    float4 Depth;
    Depth.x = HZBBuffer[GlobalIndex.x];
    Depth.y = HZBBuffer[GlobalIndex.y];
    Depth.z = HZBBuffer[GlobalIndex.z];
    Depth.w = HZBBuffer[GlobalIndex.w];
  
    float2 Depth_0 = min(Depth.xy, Depth.zw);
    float MinDepth = min(Depth_0.x, Depth_0.y);

    return MinDepth <= MaxZ;
}